[
   The sequence program in brainfuck.
   Outputs 0 to Infinity (or there-abouts)
      Limit: (Size_of_tape_in_cells - 4) / 3 ;
      Program does not terminate, it just explodes when it runs out of memory.

   Thomas DiModica, GPL3.

   Tape Structure
      \n
      0
      0
      0
      Digit 0
      Temp
      Carry Out
      Digit 1
      ...
]

++++++++++>>>>        Initialize newline
++++++++++++++++++++++++++++++++++++++++++++++++.<<<<.>>>+  print initial 0
[             forever
   [             while we are propogating a carry
      ->>+<         clear carry and set digit handled flag to false
      [             if we are carrying into a digit
         +>->+<<       increment -+ clear digit handled flag -+ set carry
         ----------------------------------------------------------
         [>>-<++++++++++<[+>-<]]>[-<+>]<   check for carry
         ++++++++++++++++++++++++++++++++++++++++++++++++>
      ]
      <[<]>>
      [             else set the next digit to 1
         -<+++++++++++++++++++++++++++++++++++++++++++++++++>
      ]
      >
   ]
   >[>>>]
   <<<[.<<<]<.   print out number big endian
   >>>+          flag a carry in
]
